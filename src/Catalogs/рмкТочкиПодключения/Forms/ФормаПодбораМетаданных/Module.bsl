#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ВыбранныеМетаданные;
	
	ЗаполнитьДоступныеМетаданные();
	Параметры.Свойство("ВыбранныеМетаданные", ВыбранныеМетаданные);
	
	Если ТипЗнч(ВыбранныеМетаданные) = Тип("Массив") Тогда
		ОтметитьВыбранныеМетаданные(ВыбранныеМетаданные);
	КонецЕсли;	
	
	ОтображатьТолькоВыбранные = Истина;
	УстановитьФильтрСтрок(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоВыбранныеПриИзменении(Элемент)
	
	УстановитьФильтрСтрок(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура Ок(Команда)
	
	Закрыть(МассивВыбранныхМетаданных());
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтметитьВыбранныеМетаданные(ВыбранныеМетаданные)
	
	ОтборМетаданные = Новый Структура("ПолноеИмя");
	
	Для Каждого ИмяМетаданных Из ВыбранныеМетаданные Цикл
		
		ОтборМетаданные.ПолноеИмя = ИмяМетаданных;
		НайденныеСтроки = ТаблицаМетаданных.НайтиСтроки(ОтборМетаданные);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].Выбран = Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеМетаданные()
	
	ДоступныеМетаданные = рмкОбщегоНазначения.МетаданныеДоступныеДляРепликации();
	
	Для Каждого ИмяМетаданных Из ДоступныеМетаданные Цикл
		
		НоваяСтрока = ТаблицаМетаданных.Добавить();
		НоваяСтрока.ПолноеИмя = ИмяМетаданных;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция МассивВыбранныхМетаданных()
	
	МассивВыбранных = Новый Массив;
	
	Для Каждого СтрокаМетаданных Из ТаблицаМетаданных Цикл
		
		Если СтрокаМетаданных.Выбран Тогда
			МассивВыбранных.Добавить(СтрокаМетаданных.ПолноеИмя);
		КонецЕсли;	
		
	КонецЦикла;
		
	Возврат МассивВыбранных;	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФильтрСтрок(Форма)
	
	Если Форма.ОтображатьТолькоВыбранные Тогда
		Форма.Элементы.ТаблицаМетаданных.ОтборСтрок = Новый ФиксированнаяСтруктура("Выбран", Истина);
	Иначе
		Форма.Элементы.ТаблицаМетаданных.ОтборСтрок = Неопределено;	
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти
